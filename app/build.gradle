/*
  Bantu Music v3 - JuTech Development
  PayChangu Payment WebView App
  
  A modern, open-source WebView wrapper for Bantu Music app.
  Built by JuTech Development for PayChangu payment integration.
  
  MIT License â€” https://opensource.org/licenses/MIT  
*/

plugins {
	id 'com.android.application'
}

def getSwvProperties() {
	def properties = new Properties()
	def propertiesFile = file('src/main/assets/swv.properties')
	if (propertiesFile.exists()) {
		properties.load(propertiesFile.newDataInputStream())
	} else {
		// Fallback or error if the file is missing
		throw new GradleException("swv.properties file not found in app/src/main/assets/")
	}
	return properties
}
def swvProps = getSwvProperties()

android {
	namespace swvProps.getProperty('build.application.id', 'com.jutech.bantumusic')
	compileSdk swvProps.getProperty('build.sdk.compile', '36').toInteger()

	defaultConfig {
		applicationId swvProps.getProperty('build.application.id', 'com.jutech.bantumusic')
		minSdk swvProps.getProperty('build.sdk.min', '24').toInteger()
		targetSdk swvProps.getProperty('build.sdk.compile', '36').toInteger()
		versionCode swvProps.getProperty('build.version.code', '1').toInteger()
		versionName swvProps.getProperty('build.version.name', '3.0.0')
		multiDexEnabled true // enable if method count exceeds 64K
	}

	buildTypes {
		release {
			minifyEnabled false // consider enabling for production
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}
	}

	buildFeatures {
		buildConfig true
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_17
		targetCompatibility JavaVersion.VERSION_17
	}

	// Optional: If you need to configure flavors
	// productFlavors {
	// }
}

dependencies {
	testImplementation 'junit:junit:4.13.2'
	androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'

	implementation fileTree(include: ['*.jar'], dir: 'libs')

	implementation 'androidx.multidex:multidex:2.0.1'

	// Core Android libraries
	implementation 'com.google.android.material:material:1.13.0'
	implementation 'androidx.core:core-splashscreen:1.2.0'
	implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
	implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.2.0'
	implementation 'androidx.appcompat:appcompat:1.7.1'
	implementation 'androidx.browser:browser:1.9.0'
	implementation 'androidx.work:work-runtime:2.11.0'
}
